// build.gradle
plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.2.2'
    id 'maven-publish'
}

// Set project version using a property (expects key: "<project.name>.version")
version = project.properties[project.name + '.version']
group = 'io.github.mcengine'

ext {
    /*
     * === User Credentials ===
     * Values pulled from environment variables to avoid hard-coding
     * sensitive information inside the repository.
     */

    // Git username used for authentication with Git-based services.
    user_git_name = System.getenv('USER_GIT_NAME')

    // GitHub account ID for identification in workflows or publishing.
    user_github_id = System.getenv('USER_GITHUB_ID')
    // GitHub display name used for metadata and publishing info.
    user_github_name = System.getenv('USER_GITHUB_NAME')
    // GitHub personal access token for reading/publishing packages.
    user_github_token = System.getenv('USER_GITHUB_TOKEN')

    // GitLab account ID for GitLab-based repositories or registries.
    user_gitlab_id = System.getenv('USER_GITLAB_ID')
    // GitLab display name used in build/publish metadata.
    user_gitlab_name = System.getenv('USER_GITLAB_NAME')
    // GitLab personal access token for package publishing.
    user_gitlab_token = System.getenv('USER_GITLAB_TOKEN')

    /*
     * === Organization Metadata ===
     * Identifies the organization associated with the module.
     * Optional values fall back to defaults when not provided.
     */

    // Optional organization identifier for registries or grouping.
    organization_id = System.getenv('ORGANIZATION_ID')
    // Name of the organization; defaults to "MCEngine-Common".
    organization_name = System.getenv('ORGANIZATION_NAME') ?: 'MCEngine-Common'
    // Organization-scoped token for accessing private GitHub Packages.
    organization_token = System.getenv('ORGANIZATION_TOKEN')

    /*
     * === Repository Metadata ===
     * Repository-level identifiers used for publishing, naming, and
     * constructing artifact coordinates.
     */

    // Optional repository ID for registry mapping.
    repository_id = System.getenv('REPOSITORY_ID')
    // Name of this repository; defaults to "economy".
    repository_name = System.getenv('REPOSITORY_NAME') ?: 'economy'
    // Repository-scoped token for GitHub Packages access.
    repository_token = System.getenv('REPOSITORY_TOKEN')

    /*
     * === Artifact Naming ===
     * Determines the base name for generated JAR files.
     */

    // Default artifact name when PROJECT_NAME is not provided.
    jarName = System.getenv('PROJECT_NAME') ?: 'MCEngineEconomy'
}

repositories {
    ext.configureGitHubRepo = { repoName, repoUrl ->
        maven {
            name = repoName
            url = uri(repoUrl)
            credentials {
                username = user_github_name
                password = user_github_token ?: repository_token ?: organization_token
            }
        }
    }

    configureGitHubRepo('MCEngine Core API', 'https://maven.pkg.github.com/MCEngine-API/core')
    configureGitHubRepo('MCEngine Economy API', 'https://maven.pkg.github.com/MCEngine-API/economy')

    mavenCentral()
    mavenLocal()

    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }

    maven {
        name = "papermc"
        url = 'https://repo.papermc.io/repository/maven-public/'
    }

    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.21.10-R0.1-SNAPSHOT'

    compileOnly 'com.google.code.gson:gson:2.13.2'

    compileOnly 'io.github.mcengine:core-api:2025.1.1-22'
    compileOnly 'io.github.mcengine:economy-api:2025.1.1-22'
}

shadowJar {
    version = version
    archiveClassifier = null
    archiveBaseName = jarName
}

tasks.named('jar').configure {
    enabled = false
}

publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri("https://maven.pkg.github.com/${organization_name}/${repository_name}")
            credentials {
                username = user_git_name
                password = user_github_token ?: repository_token ?: organization_token
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifact tasks.named('shadowJar').get()
            artifactId = "${repository_name}-common"

            pom {
                name.set(jarName)
                description.set("The ${jarName} API is designed for use by Economy plugin.")
                url.set("https://github.com/${organization_name}/${repository_name}")

                scm {
                    connection.set("scm:git:git://github.com/${organization_name}/${repository_name}.git")
                    developerConnection.set("scm:git:ssh://github.com:${organization_name}/${repository_name}.git")
                    url.set("https://github.com/${organization_name}/${repository_name}")
                }

                developers {
                    developer {
                        id.set('jetsadawijit')
                        name.set('Jetsada Wijit')
                        email.set('jetsadawijit@outlook.com')
                        url.set('https://jetsadawijit.github.io')
                    }
                }
            }
        }
    }
}

build.dependsOn(shadowJar)
